/*
 * Copyright 2005-present the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id "io.freefair.aggregate-javadoc" version "8.12.1"
	id "org.asciidoctor.jvm.convert" version "4.0.4"
	id "org.asciidoctor.jvm.pdf" version "4.0.4"
	id "maven-publish"
	id "org.springframework.ws.conventions"
}

repositories {
	mavenCentral()
}

configurations {
	asciidoctorExt
}

dependencies {
	asciidoctorExt("io.spring.asciidoctor.backends:spring-asciidoctor-backends:0.0.5")
	javadoc(enforcedPlatform(project(":spring-ws-platform")))
	[":spring-ws-core", ":spring-ws-security", ":spring-ws-support", ":spring-ws-test", ":spring-xml"].each {
		javadoc(project(it))
		javadoc(project(path: it, configuration: "javadocClasspath"))
	}
}

asciidoctorj {
	attributes = [
		"allow-uri-read": true,
		"numbered": true,
		"spring-framework-version": springFrameworkVersion,
		"spring-ws-version": project.version,
		"toclevels": 4
	]
	options = [
		"doctype": "book",
	]
}

tasks.named("asciidoctor") {
	sources {
		include "index.adoc"
	}
	configureOutputOptions {
		backends = ["spring-html"]
	}
	baseDirFollowsSourceFile()
	configurations("asciidoctorExt")
}

tasks.named("asciidoctorPdf") {
	sources {
		include "index.adoc"
	}
	baseDirFollowsSourceFile()
}

tasks.named("javadoc") {
	title = "Spring Web Services ${project.version} API"
	options {
		use = true
	}
}

def zip = tasks.register("zip", Zip) {
	archiveBaseName = project.name
	archiveVersion = project.version
	destinationDirectory = project.layout.buildDirectory
	into("reference/html") {
		from asciidoctor
	}
	into ("reference/pdf") {
		from asciidoctorPdf
	}
	into ("api") {
		from javadoc
	}
}

tasks.named("assemble") {
	dependsOn(zip)
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact zip
		}
	}
}
